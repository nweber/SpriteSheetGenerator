<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication
	xmlns:fx="http://ns.adobe.com/mxml/2009" 
	xmlns:s="library://ns.adobe.com/flex/spark"
	xmlns:mx="library://ns.adobe.com/flex/mx"
	width="500" height="625">
	
	<fx:Script>
		<![CDATA[
			import mx.controls.Alert;
			
			private var pngDirectory:File;
			private var saveFile:File;
			
			private var fileQueue:Array;
			private var imageDatas:Array;
			
			private var currentImageFile:File;
			private var imageLoader:Loader;
			
			private var encoder:PNGEncoder2;

			private var output:BitmapData;
			private var numColumns:int;
			private var numRows:int;
			private var columnCount:int;
			private var rowCount:int;
			private var imageWidth:Number;
			private var imageHeight:Number;
			private var columnWidth:Number;
			private var rowHeight:Number;
			private var scale:Number;
			private var verticalOffset:Number;
			private var horizontalOffset:Number;

			
			private function handleSelectDirectory( event:MouseEvent ):void {
				pngDirectory = new File();
				pngDirectory.addEventListener( Event.SELECT, handleBrowseSelect );
				pngDirectory.addEventListener( Event.CANCEL, handleBrowseCancel );
				pngDirectory.browseForDirectory( "Selecy Directory" );
			}
			
			private function handleBrowseSelect( event:Event ):void {
				pngDirectory.removeEventListener( Event.SELECT, handleBrowseSelect );
				pngDirectory.removeEventListener( Event.CANCEL, handleBrowseCancel );
				pngDirectoryDisplay.text = pngDirectory.nativePath;
			}
			
			private function handleBrowseCancel( event:Event ):void {
				pngDirectory.removeEventListener( Event.SELECT, handleBrowseSelect );
				pngDirectory.removeEventListener( Event.CANCEL, handleBrowseCancel );
				pngDirectory = null;
				pngDirectoryDisplay.text = null;
			}
			
			private function handleCreateClick( event:MouseEvent ):void {
				if ( !pngDirectory ) {
					Alert.show( "Please select a PNG directory.", "Error" );
					return;
				}
				
				saveFile = new File();
				saveFile.addEventListener( Event.SELECT, handleSaveSelect );
				saveFile.browseForSave( "Select Save Destination" );
			}
			
			private function handleSaveSelect( event:Event ):void {
				saveFile.removeEventListener( Event.SELECT, handleSaveSelect );
				
				if ( saveFile.extension != "png" ) {
					var idx:int = saveFile.nativePath.lastIndexOf( "." );
					var path:String = ( idx == -1 ) ? saveFile.nativePath : saveFile.nativePath.substring( 0, idx );
					saveFile.nativePath = path + ".png";
				}
				
				startBitmapGeneration();
			}
			
			private function startBitmapGeneration():void {
				fileQueue = pngDirectory.getDirectoryListing();
				fileQueue.reverse();
				imageDatas = [];
				
				loadingDisplay.visible = true;
				processFileQueue();
 			}
			
			private function processFileQueue():void {
				if ( fileQueue.length > 0 ) {
					currentImageFile = fileQueue.pop();
					
					imageLoader = new Loader();
					imageLoader.contentLoaderInfo.addEventListener( Event.COMPLETE, handleImageLoadComplete );
					imageLoader.contentLoaderInfo.addEventListener( IOErrorEvent.IO_ERROR, handleImageLoadError );
					imageLoader.load( new URLRequest( currentImageFile.nativePath ) );
				}
				else {
					generateBitmap();
				}
			}
			
			private function handleImageLoadComplete( event:Event ):void {
				imageDatas.push( ( imageLoader.content as Bitmap).bitmapData );
				
				currentImageFile = null;
				imageLoader.contentLoaderInfo.removeEventListener( Event.COMPLETE, handleImageLoadComplete );
				imageLoader.contentLoaderInfo.removeEventListener( IOErrorEvent.IO_ERROR, handleImageLoadError );
				imageLoader = null;
				
				processFileQueue();
			}
			
			private function handleImageLoadError( event:Event ):void {
				Alert.show( "There was an error loading file: " + currentImageFile.nativePath, "Error" );
				
				currentImageFile = null;
				imageLoader.contentLoaderInfo.removeEventListener( Event.COMPLETE, handleImageLoadComplete );
				imageLoader.contentLoaderInfo.removeEventListener( IOErrorEvent.IO_ERROR, handleImageLoadError );
				imageLoader = null;
				
				clear();
			}
			
			private function generateBitmap():void {
				numColumns = columnStepper.value;
				numRows = rowStepper.value;
				
				if ( layoutGroup.selectedValue == "horizontal" ) {
					numColumns = imageDatas.length;
					numRows = 1;
				}
				else if ( layoutGroup.selectedValue == "vertical" ) {
					numColumns = 1;
					numRows = imageDatas.length;
				}
				else {
					numColumns = columnStepper.value;
					numRows = rowStepper.value;
				}
				
				imageWidth = (imageDatas[0] as BitmapData).width;
				imageHeight = (imageDatas[0] as BitmapData).height;
				
				if ( sizeGroup.selectedValue == "custom" ) {
					columnWidth = cellWidthStepper.value;
					rowHeight = cellHeightStepper.value;
					
					var vScale:Number = rowHeight / imageHeight;
					var hScale:Number = columnWidth / imageWidth;
					scale = Math.max( vScale, hScale );
				}
				else {
					columnWidth = imageWidth;
					rowHeight = imageHeight;
					scale = 1;
				}
				
				verticalOffset = cellPaddingVerticalStepper.value;
				horizontalOffset = cellPaddingHorizontalStepper.value;
				
				columnWidth += horizontalOffset;
				rowHeight += verticalOffset;
				
				var width:Number = numColumns * columnWidth;
				var height:Number = numRows * rowHeight;
				
				output = new BitmapData( width, height );
				columnCount = 0;
				rowCount = 0;
				
				// Wrap in a call later to execute next frame.
				// If we do it all at once the application will lock up and the progress bar will stop.
				callLater( processBitmapQueue );
			}
			
			private function processBitmapQueue():void {
				if ( imageDatas.length > 0 ) {
					var sourceBmp:BitmapData;
					var sourceMtx:Matrix;
					var sourceRect:Rectangle;
					var destX:Number;
					var destY:Number;
					var destPoint:Point;
					
					var data:BitmapData = imageDatas.splice( 0, 1 )[0];
					
					if ( scale == 1 ) {
						sourceBmp = data;
					}
					else {
						sourceMtx = new Matrix();
						sourceMtx.scale( scale, scale );
						
						sourceBmp = new BitmapData( data.width * scale, data.height * scale );
						sourceBmp.draw( data, sourceMtx );
					}
					
					sourceRect = new Rectangle( 0, 0, sourceBmp.width, sourceBmp.height );
					
					destX = ( columnCount * columnWidth ) - ( horizontalOffset / 2 );
					destY = ( rowCount * rowHeight ) - ( verticalOffset / 2 );
					
					destPoint = new Point( destX, destY );
					
					output.copyPixels( sourceBmp, sourceRect, destPoint );
					
					columnCount++;
					
					if ( columnCount >= numColumns ) {
						columnCount = 0;
						rowCount++;
					}
					
					// Wrap in a call later to execute next frame.
					// If we do it all at once the application will lock up and the progress bar will stop.
					callLater( processBitmapQueue );
				}
				else {
					saveBitmap( output );
				}
			}
			
			private function saveBitmap( bmp:BitmapData ):void {
				PNGEncoder2.level = CompressionLevel.GOOD;
				encoder = PNGEncoder2.encodeAsync( bmp );
				encoder.addEventListener( Event.COMPLETE, handleEncodeComplete );
			}
			
			private function handleEncodeComplete( event:Event ):void {
				encoder.removeEventListener( Event.COMPLETE, handleEncodeComplete );
				
				var png:ByteArray = encoder.png;
				
				var stream:FileStream = new FileStream();
				stream.open( saveFile, FileMode.WRITE );
				stream.writeBytes( png );
				stream.close();
				
				clear();
				
				loadingDisplay.visible = false;
			}
			
			private function clear():void {
				saveFile = null;
			}
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<s:RadioButtonGroup
			id="layoutGroup"/>
		
		<s:RadioButtonGroup
			id="sizeGroup"
			selectedValue="image"/>
	</fx:Declarations>
	
	<s:Form top="0" bottom="0" left="0" right="0">
		
		<s:FormItem label="PNG Directory">
			<s:Button
				label="Select"
				click="handleSelectDirectory(event)"/>
			<s:TextInput
				id="pngDirectoryDisplay"
				editable="false"
				selectable="false"
				width="250"/>
		</s:FormItem>
				
		<s:FormItem label="Layout">
			<s:RadioButton
				label="Horizontal Line"
				value="horizontal"
				group="{layoutGroup}"/>
			
			<s:RadioButton
				label="Vertical Line"
				value="vertical"
				group="{layoutGroup}"/>
			
			<s:RadioButton
				label="Custom"
				value="custom"
				group="{layoutGroup}"/>
		</s:FormItem>
		
		<s:FormItem label="Number of Rows" enabled="{layoutGroup.selectedValue == 'custom'}">
			<s:NumericStepper
				id="rowStepper"
				minimum="1" maximum="99" value="1"/>
		</s:FormItem>
		
		<s:FormItem label="Number of Columns" enabled="{layoutGroup.selectedValue == 'custom'}">
			<s:NumericStepper
				id="columnStepper"
				minimum="1" maximum="99" value="1"/>
		</s:FormItem>
		
		<s:FormItem label="Cell Padding Horizontal">
			<s:NumericStepper
				id="cellPaddingHorizontalStepper"
				minimum="0" maximum="9999" value="0"/>
		</s:FormItem>
		
		<s:FormItem label="Cell Padding Vertical">
			<s:NumericStepper
				id="cellPaddingVerticalStepper"
				minimum="0" maximum="9999" value="0"/>
		</s:FormItem>
		
		<s:FormItem label="Cell Size">
			<s:RadioButton
				label="Image Size"
				value="image"
				group="{sizeGroup}"/>
			
			<s:RadioButton
				label="Custom"
				value="custom"
				group="{sizeGroup}"/>
		</s:FormItem>
		
		<s:FormItem label="Cell Width" enabled="{sizeGroup.selectedValue == 'custom'}">
			<s:NumericStepper
				id="cellWidthStepper"
				minimum="1" maximum="9999" value="100"/>
		</s:FormItem>
		
		<s:FormItem label="Cell Height" enabled="{sizeGroup.selectedValue == 'custom'}">
			<s:NumericStepper
				id="cellHeightStepper"
				minimum="1" maximum="9999" value="100"/>
		</s:FormItem>
		
		<s:FormItem>
			<s:Button
				label="Create Sprite Sheet"
				click="handleCreateClick(event)"/>
		</s:FormItem>
		
	</s:Form>
	
	<s:Group
		id="loadingDisplay"
		top="0" bottom="0" left="0" right="0"
		visible="false">
		
		<s:Rect width="100%" height="100%">
			<s:fill>
				<s:SolidColor color="black" alpha=".75"/>
			</s:fill>
		</s:Rect>
		
		<mx:ProgressBar
			indeterminate="true"
			width="300"
			color="white"
			verticalCenter="0"
			horizontalCenter="0"/>
		
	</s:Group>
	
</s:WindowedApplication>
